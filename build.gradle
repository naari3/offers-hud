plugins {
    id "dev.kikugie.stonecutter"
    id 'fabric-loom' version '1.14-SNAPSHOT' apply false
    id 'net.neoforged.moddev' version '2.0.131' apply false
    id 'maven-publish'
}

def current = stonecutter.current.project
def isNeoForge = current.endsWith("-neoforge")
def isFabric = !isNeoForge

if (isNeoForge) {
    stonecutter.const("neoforge", true)
    stonecutter.const("fabric", false)
} else {
    stonecutter.const("fabric", true)
    stonecutter.const("neoforge", false)
}

// Extract MC version from project name
def mcVersion = current.replace("-fabric", "").replace("-neoforge", "")

// Apply loader-specific plugin
if (isFabric) {
    apply plugin: 'fabric-loom'
} else {
    apply plugin: 'net.neoforged.moddev'
}

base {
    archivesName = project.archives_base_name
}
version = project.mod_version + '+' + stonecutter.current.project
group = project.maven_group

def javaInt = isFabric
    ? (stonecutter.eval(mcVersion, ">=1.20.5") ? 21 : 17)
    : 21

repositories {
    mavenCentral()
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://maven.parchmentmc.org" }
}

if (isFabric) {
    dependencies {
        minecraft "com.mojang:minecraft:${mcVersion}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${property('parchment_version')}@zip")
        }
        modImplementation "net.fabricmc:fabric-loader:${property('loader_version')}"
        modImplementation "net.fabricmc.fabric-api:fabric-api:${property('fabric_version')}"

        modImplementation("com.terraformersmc:modmenu:${property('modmenu_version')}") {
            exclude module: 'fabric-api'
        }
        modApi("me.shedaniel.cloth:cloth-config-fabric:${property('cloth_config_version')}") {
            exclude module: 'fabric-api'
        }
    }

    loom {
        runConfigs.all {
            ideConfigGenerated true
            runDir "../../run"
        }
    }

    processResources {
        def mcDepVersion = project.minecraft_deps
        inputs.property "version", version
        inputs.property "mc_dep_version", mcDepVersion
        inputs.property "java_version", javaInt

        def vars = [
                "version": version,
                "mc_dep_version": mcDepVersion,
                "java_version": javaInt
        ]
        filesMatching("fabric.mod.json") {
            expand(vars)
        }
        filesMatching("offershud.mixins.json") {
            expand(vars)
        }
    }
} else {
    // NeoForge configuration
    neoForge {
        version = property('neoforge_version')

        parchment {
            minecraftVersion = property('parchment_mc_version')
            mappingsVersion = property('parchment_mapping_version')
        }

        runs {
            client {
                client()
            }
        }

        mods {
            offershud {
                sourceSet sourceSets.main
            }
        }
    }

    dependencies {
        implementation("me.shedaniel.cloth:cloth-config-neoforge:${property('cloth_config_version')}")
    }

    processResources {
        def vars = [
                "version": version,
                "mc_version": mcVersion,
                "neoforge_version_range": findProperty('neoforge_version_range') ?: '[0,)',
                "loader_version_range": findProperty('loader_version_range') ?: '[0,)',
                "java_version": javaInt
        ]
        filesMatching("META-INF/neoforge.mods.toml") {
            expand(vars)
        }
        filesMatching("offershud.mixins.json") {
            expand(vars)
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = javaInt
}

java {
    withSourcesJar()
    def javaObj = javaInt == 21 ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17
    sourceCompatibility = javaObj
    targetCompatibility = javaObj
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
    }
}
